plugins {
    id "io.codearte.nexus-staging" version "0.30.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

ext {
    // Minimum required Testerra version
    testerraCompileVersion = '1.0-RC-34'
    // Unit tests use the latest Testerra version
    testerraTestVersion = '[1.0-RC,2-SNAPSHOT)'
    moduleVersion = '1-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }

    group 'io.testerra'
    version moduleVersion
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'io.testerra:driver-ui-desktop:' + testerraCompileVersion
    implementation 'org.apache.httpcomponents:httpclient:4.5.12'
    implementation 'com.google.code.gson:gson:2.8.6'

    testImplementation 'io.testerra:driver-ui-desktop:' + testerraTestVersion
    testImplementation 'io.testerra:report-ng:' + testerraTestVersion
}

test {
    useTestNG()

    options {
        systemProperties(System.getProperties())
    }
}

